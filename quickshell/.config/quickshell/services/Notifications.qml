pragma Singleton

import Quickshell
import Quickshell.Io
import Quickshell.Services.Notifications
import Quickshell.Wayland
import QtQuick

Singleton {
    id: root

    property bool dnd: false
    property bool centerOpen: false

    property var list: []
    property var popups: []
    property var exiting: []

    readonly property bool hasCritical: list.some(n => n.urgency === NotificationUrgency.Critical)

    property var _times: ({})
    property var _exitAt: ({})
    property var _shownAt: ({})
    property var _held: ({})
    property date now: new Date()

    onCenterOpenChanged: {
        if (centerOpen) {
            now = new Date()
            popups = []
            exiting = []
        }
    }

    readonly property int maxTimeout: 30000

    function timeoutFor(n) {
        if (n.urgency === NotificationUrgency.Critical)
            return n.transient ? 10000 : 0
        const floor = n.urgency === NotificationUrgency.Low ? 5000 : 10000
        const requested = n.expireTimeout > 0 ? n.expireTimeout : 0
        return Math.min(maxTimeout, Math.max(floor, requested))
    }

    function bypassesDnd(n) {
        return n.urgency === NotificationUrgency.Critical && (n.appName || "") === "notify-send"
    }

    function invokeDefault(n) {
        if (!_alive(n)) return
        const action = [...n.actions].find(a => a.identifier === "default")
        if (action) action.invoke()
        else focusSender(n)
        n.dismiss()
    }

    function hidePopup(n) {
        if (!popups.includes(n) || exiting.includes(n))
            return
        if (n.transient) {
            n.dismiss()
            return
        }
        _stageExit(n)
    }

    function _stageExit(n) {
        _exitAt[n.id] = Date.now()
        exiting = [...exiting, n]
    }

    function finalizeHide(n) {
        if (_alive(n)) {
            delete _exitAt[n.id]
            delete _shownAt[n.id]
            delete _held[n.id]
        }
        popups = popups.filter(p => p !== n)
        exiting = exiting.filter(p => p !== n)
    }

    function hold(n, held) {
        if (_alive(n))
            _held[n.id] = held
    }

    function _alive(n) {
        try {
            return n !== null && n !== undefined && n.id !== undefined
        } catch (e) {
            return false
        }
    }

    function prune() {
        list = list.filter(p => _alive(p))
        popups = popups.filter(p => _alive(p))
        exiting = exiting.filter(p => _alive(p))
    }

    function clearAll() {
        const pending = [...list]
        for (const n of pending)
            n.dismiss()
    }

    function focusSender(n) {
        if (!_alive(n)) return
        const app = (n.appName || "").toLowerCase()
        if (!app) return
        const match = [...ToplevelManager.toplevels.values].find(t => {
            const id = (t.appId || "").toLowerCase()
            return id && (id.includes(app) || app.includes(id))
        })
        if (match) match.activate()
    }

    function ago(id) {
        const t = _times[id]
        if (!t)
            return ""
        const s = Math.max(0, Math.floor((now.getTime() - t) / 1000))
        if (s < 60) return "now"
        if (s < 3600) return Math.floor(s / 60) + "m"
        if (s < 86400) return Math.floor(s / 3600) + "h"
        return Math.floor(s / 86400) + "d"
    }

    function _adopt(n, fresh) {
        n.tracked = true
        _times[n.id] = Date.now()
        n.closed.connect(() => root._drop(n))
        _watchUpdates(n)

        if (!n.transient && !list.includes(n))
            list = [n, ...list]

        if (fresh && (!dnd || bypassesDnd(n)) && !centerOpen && !popups.includes(n)) {
            _shownAt[n.id] = Date.now()
            popups = [n, ...popups]
        } else if (fresh && n.transient)
            Qt.callLater(() => n.dismiss())
    }

    function _watchUpdates(n) {
        const refresh = () => root._touch(n)
        for (const sig of ["summaryChanged", "bodyChanged", "imageChanged"]) {
            const s = n[sig]
            if (s && typeof s.connect === "function")
                s.connect(refresh)
        }
    }

    function _touch(n) {
        if (!_alive(n) || exiting.includes(n))
            return
        if (popups.includes(n)) {
            _shownAt[n.id] = Date.now()
            return
        }
        if ((dnd && !bypassesDnd(n)) || centerOpen || n.transient)
            return
        _shownAt[n.id] = Date.now()
        popups = [n, ...popups]
    }

    property bool _hydrated: false

    readonly property var _state: FileView {
        path: Quickshell.stateDir + "/notifications.json"
        atomicWrites: true
        printErrors: false
        onLoaded: root._hydrate(text())
        onLoadFailed: root._hydrate("")
    }

    function _hydrate(raw) {
        if (_hydrated) return
        try {
            const parsed = JSON.parse(raw)
            if (parsed && typeof parsed.dnd === "boolean")
                dnd = parsed.dnd
        } catch (e) {}
        _hydrated = true
    }

    onDndChanged: if (_hydrated) _state.setText(JSON.stringify({ dnd: dnd }) + "\n")

    function _drop(n) {
        list = list.filter(p => p !== n)
        if (popups.includes(n) && !exiting.includes(n))
            _stageExit(n)
        delete _times[n.id]
    }

    readonly property var _ipc: IpcHandler {
        target: "notifs"

        function dnd(): string {
            return root.dnd ? "on" : "off"
        }

        function toggleDnd(): string {
            root.dnd = !root.dnd
            return dnd()
        }

        function dismiss(): string {
            const visible = root.popups.filter(n => !root.exiting.includes(n))
            if (visible.length === 0) return "none"
            root.hidePopup(visible[0])
            return "ok"
        }

        function dismissAll(): string {
            for (const n of [...root.popups])
                root.hidePopup(n)
            return "ok"
        }

        function invokeLast(): string {
            const visible = root.popups.filter(n => !root.exiting.includes(n))
            if (visible.length === 0) return "none"
            root.invokeDefault(visible[0])
            return "ok"
        }

        function clearAll(): string {
            root.clearAll()
            return "ok"
        }
    }

    readonly property var server: NotificationServer {
        keepOnReload: true
        actionsSupported: true
        bodySupported: true
        bodyMarkupSupported: true
        bodyHyperlinksSupported: true
        imageSupported: true
        persistenceSupported: true

        onNotification: (n) => root._adopt(n, true)
    }

    readonly property var _tick: Timer {
        interval: 30000
        running: root.centerOpen
        repeat: true
        onTriggered: root.now = new Date()
    }

    readonly property var _locks: Variants {
        model: root.popups
        delegate: RetainableLock {
            required property var modelData
            object: modelData
            locked: true
        }
    }

    readonly property var _expire: Timer {
        interval: 500
        repeat: true
        running: root.popups.length > root.exiting.length
        onTriggered: {
            const t = Date.now()
            for (const n of [...root.popups]) {
                if (!root._alive(n)) {
                    root.finalizeHide(n)
                    continue
                }
                if (root.exiting.includes(n))
                    continue
                const timeout = root.timeoutFor(n)
                if (timeout <= 0)
                    continue
                if (root._held[n.id]) {
                    root._shownAt[n.id] = t
                    continue
                }
                if (t - (root._shownAt[n.id] ?? t) > timeout)
                    root.hidePopup(n)
            }
        }
    }

    readonly property var _sweep: Timer {
        interval: 1000
        repeat: true
        running: root.exiting.length > 0
        onTriggered: {
            const cutoff = Date.now() - 1000
            for (const n of [...root.exiting]) {
                if (!root._alive(n) || (root._exitAt[n.id] ?? 0) < cutoff)
                    root.finalizeHide(n)
            }
        }
    }

    Component.onCompleted: {
        const vals = server.trackedNotifications.values
        for (const n of vals)
            _adopt(n, false)
    }
}
